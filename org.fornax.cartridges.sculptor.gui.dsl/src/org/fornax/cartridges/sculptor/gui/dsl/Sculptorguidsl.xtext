grammar org.fornax.cartridges.sculptor.gui.dsl.Sculptorguidsl with org.eclipse.xtext.common.Terminals

import "http://www.fornax.org/cartridges/sculptor/dsl/sculptordsl" as mm

generate sculptorguidsl "http://www.fornax.org/cartridges/sculptor/gui/dsl/sculptorguidsl"

DslGuiApplication :
	(imports+=DslImport)*
	(doc=STRING)?
	'gui' name=ID 'for' guiForApplication=[mm::DslApplication]  '{'
		("hint" "=" hint=STRING)?
	   //TODO - later release
	   //(topUserTasks+=DslUserTask)*
       (modules+=DslGuiModule)*
	'}';
	
DslImport :
	'import' importURI=STRING;
	
DslGuiModule:
	'Module' 'for' for=[mm::DslModule] '{'
		("basePackage" "=" basePackage=DslGuiJavaIdentifier )?
		("hint" "=" hint=STRING)?
		(serviceDependencies+=DslGuiServiceDependency)*
		(skipDomainObjects+=DslSkipDomainObject)*
		(views+=DslView)*
		(events+=DslGuiEvent)*
		(behaviors+=DslUiBehavior)*
		(conditions+=DslUiCondition)*
		(dtos+=DslGuiDto)*
	'}';

	 


DslSkipDomainObject :
	'skip' do=[mm::DslSimpleDomainObject];


DslView:
  (doc=STRING)?
	"View" name=ID ("extends" (("@"extends=[DslView]) | (extendsName=DslGuiJavaIdentifier)))? ('for' for=[mm::DslSimpleDomainObject])? "{"
		("hint" "=" hint=STRING)?
	
		('label' '=' label=STRING)?
		
		((popup?="popup") | (NOT "popup"))?
		
		(serviceDependencies+=DslGuiServiceDependency)*
		(publishedEvents+=DslPublishedEvent)*

		("parameters" "{"
			(parameters+=DslViewParameter)*
		"}"
		)?

		("contents" "{"
			(widgets+=DslWidget)*
		"}"		
		)?

		(behaviors+=DslUiBehavior)*
		(conditions+=DslUiCondition)*
		
	'}';

DslViewParameter :
  (doc=STRING)?
  (type=DslGuiType  name=ID ((nullable?="nullable") | (NOT "nullable"))?
	) (";")?;

DslPublishedEvent :
	'publish' event=[mm::DslEvent];

DslGuiServiceDependency :
  ('>'|'inject') ("@"dependency=[mm::DslService]);


///////////////////////////////////////////////////////////////////////////////////////
// Widget definitions
//////////////////////////////////////////////////////////////////////////////////////

// This isn't pulling up the 'for' feature for some reason.  Look into making these common.
// TODO: Whenever adding a widget that has a 'for' reference to a domain obj, update Validator to validate it
DslSourceReferringWidget :
	DslInputDateWidget | DslNumberSpinnerWidget;

DslWidget : 
	DslInputTextWidget | DslButtonWidget | DslInformationalTextWidget | DslTableWidget | DslInputDateWidget | DslNumberSpinnerWidget | DslTextAreaWidget
	| DslRichTextAreaWidget | DslAutocompleteWidget | DslRadioButtonsWidget | DslLinkWidget | DslPanelWidget | DslCustomWidget | DslListBoxWidget;
	

DslPanelWidget :
  (doc=STRING)?
	"panel" name=ID ('for' for=[mm::DslSimpleDomainObject])? "{"
		("hint" "=" hint=STRING)?
		('label' '=' label=STRING)?
		('widgetType' '=' widgetType=STRING)?
		((disclosure?="disclosure") | (NOT "disclosure"))?
		('enabled' enabled=[DslUiCondition])?
		('visible' visible=[DslUiCondition])?
		(behaviorBindings+=DslOnHover)?
		("contents" "{"
			(widgets+=DslWidget)*
		"}"
		)?
	'}';
		
DslInputTextWidget :
  (doc=STRING)?
	'inputText' name=ID  (sourceRef = DslSourceReference)? '{'
		("hint" "=" hint=STRING)?
		('label' '=' label=STRING)?
		('widgetType' '=' widgetType=STRING)?
		('enabled' enabled=[DslUiCondition])?
		('visible' visible=[DslUiCondition])?
		(behaviorBindings+=DslOnHover)?
	'}';
	
DslListBoxWidget :
  (doc=STRING)?
	'listBox' name=ID  ('for' for=[mm::DslSimpleDomainObject])? (sourceRef = DslSourceReference)? ('forEnum' forEnum=[mm::DslEnum])? '{'
		("hint" "=" hint=STRING)?
		('label' '=' label=STRING)?
		('widgetType' '=' widgetType=STRING)?
		('enabled' enabled=[DslUiCondition])?
		('visible' visible=[DslUiCondition])?
		(behaviorBindings+=DslOnSelectItem)?
		(behaviorBindings+=DslOnHover)?
	'}';

DslTextAreaWidget :
  (doc=STRING)?
	'textArea' name=ID  (sourceRef = DslSourceReference)? '{'
		("hint" "=" hint=STRING)?
		('label' '=' label=STRING)?
		('widgetType' '=' widgetType=STRING)?
		('enabled' enabled=[DslUiCondition])?
		('visible' visible=[DslUiCondition])?
		(behaviorBindings+=DslOnHover)?
	'}';

DslAutocompleteWidget :
  (doc=STRING)?
	'autocomplete' name=ID  ('for' for=[mm::DslSimpleDomainObject])? (sourceRef = DslSourceReference)? '{'
		("hint" "=" hint=STRING)?
		('label' '=' label=STRING)?
		('widgetType' '=' widgetType=STRING)?
		('enabled' enabled=[DslUiCondition])?
		('visible' visible=[DslUiCondition])?
		(behaviorBindings+=DslOnSelectItem)?
		(behaviorBindings+=DslOnHover)?
	'}';

DslRadioButtonsWidget :
  (doc=STRING)?
	'radioButtons' name=ID  (sourceRef = DslSourceReference)? '{'
		("hint" "=" hint=STRING)?
		('label' '=' label=STRING)?
		('widgetType' '=' widgetType=STRING)?
		('enabled' enabled=[DslUiCondition])?
		('visible' visible=[DslUiCondition])?
		(behaviorBindings+=DslOnHover)?
	'}';

DslRichTextAreaWidget :
  (doc=STRING)?
	'richText' name=ID  (sourceRef = DslSourceReference)? '{'
		("hint" "=" hint=STRING)?
		('label' '=' label=STRING)?
		('widgetType' '=' widgetType=STRING)?
		('enabled' enabled=[DslUiCondition])?
		('visible' visible=[DslUiCondition])?
		(behaviorBindings+=DslOnHover)?
	'}';

	
DslInputDateWidget :
  (doc=STRING)?
	'inputDate' name=ID  (sourceRef = DslSourceReference)? '{'
		("hint" "=" hint=STRING)?
		('label' '=' label=STRING)?
		('widgetType' '=' widgetType=STRING)?
		('enabled' enabled=[DslUiCondition])?
		('visible' visible=[DslUiCondition])?
		(behaviorBindings+=DslOnHover)?
	'}';
 
 DslNumberSpinnerWidget :
 (doc=STRING)?
	'numberSpinner' name=ID  (sourceRef = DslSourceReference)? '{'
		("hint" "=" hint=STRING)?
		('label' '=' label=STRING)?
		('widgetType' '=' widgetType=STRING)?
		('enabled' enabled=[DslUiCondition])?
		('visible' visible=[DslUiCondition])?
		(behaviorBindings+=DslOnHover)?
	'}';
 

	
DslButtonWidget :
  (doc=STRING)?
	'button' name=ID (sourceRef = DslSourceReference)? '{'
		("hint" "=" hint=STRING)?
		('label' '=' label=STRING)?
		('image' '=' image=STRING)?
		('widgetType' '=' widgetType=STRING)?
		('enabled' enabled=[DslUiCondition])?
		('visible' visible=[DslUiCondition])?
		(behaviorBindings+=DslOnClick)?
		(behaviorBindings+=DslOnHover)?
	'}';

DslInformationalTextWidget :
  (doc=STRING)?
	'informationalText' name=ID (sourceRef = DslSourceReference)? '{'
		("hint" "=" hint=STRING)?
		('label' '=' label=STRING)?
		('widgetType' '=' widgetType=STRING)?
		('enabled' enabled=[DslUiCondition])?
		('visible' visible=[DslUiCondition])?
		(behaviorBindings+=DslOnHover)?
	'}';



DslSourceReference :
	('forOp' forOp=[mm::DslDomainObjectOperation]) | ('forProperty' forProperty=[mm::DslProperty]) | ('forPath' forPath=STRING);


DslLinkWidget :
  (doc=STRING)?
	'link' name=ID (sourceRef = DslSourceReference)? ('toView' toView=[DslView])? ('toDestination' toDestination=STRING)? '{'
		("hint" "=" hint=STRING)?
		('label' '=' label=STRING)?
		('widgetType' '=' widgetType=STRING)?
		('enabled' enabled=[DslUiCondition])?
		('visible' visible=[DslUiCondition])?
		(behaviorBindings+=DslOnClick)?
		(behaviorBindings+=DslOnHover)?
	'}';
	
DslForProperty :
	'forProperty' domainObj=[mm::DslSimpleDomainObject] '.' property=[mm::DslProperty];

DslTableWidget :
  (doc=STRING)?
	'table' name=ID ('for' for=[mm::DslSimpleDomainObject])? (sourceRef = DslSourceReference)? '{'
		("hint" "=" hint=STRING)?
		('label' '=' label=STRING)?	
		('widgetType' '=' widgetType=STRING)?		
		('enabled' enabled=[DslUiCondition])?
		('visible' visible=[DslUiCondition])?
		("size" '=' size=INT)?
		((paging?="paging") | (NOT "paging"))?
		(behaviorBindings+=DslOnHover)?
		("columns" "{"
			(columns+=DslTableColumn)*
		"}")?
	'}';
	
DslTableColumn : DslSimpleTableColumn | DslWidgetTableColumn;

DslSimpleTableColumn :
	name=ID ("hint" "=" hint=STRING)? ('label' '=' label=STRING)? ('type' '=' columnType=DslColumnType)? (sourceRef = DslSourceReference)?
		((filterable?="filterable") | (NOT "filterable"))? ((sortable?="sortable") | (NOT "sortable"))?
		((editable?="editable") | (NOT "editable"))?
		(behaviorBindings+=DslOnClick)? (behaviorBindings+=DslOnHover)? ";" ;

DslWidgetTableColumn :
	'widget' ("hint" "=" hint=STRING)? ((filterable?="filterable") | (NOT "filterable"))? ((sortable?="sortable") | (NOT "sortable"))?
		((editable?="editable") | (NOT "editable"))?
		widget=DslWidget;



DslCustomWidget :
  (doc=STRING)?
	'custom' name=ID  (sourceRef = DslSourceReference)? '{'
		("hint" "=" hint=STRING)?
		('label' '=' label=STRING)?
		('widgetType' '=' widgetType=STRING)?
		('enabled' enabled=[DslUiCondition])?
		('visible' visible=[DslUiCondition])?
		(behaviorBindings+=DslOnHover)?
	'}';
	
/////////////////////////////////////////////////////////////////////////
// Behavior bindings
/////////////////////////////////////////////////////////////////////////

DslBehaviorBinding :
	DslOnClick | DslOnHover | DslOnSelectItem;
	
DslOnClick :
	'onClick' '=' behaviors+=[DslUiBehavior] ("," behaviors+=[DslUiBehavior])*;

DslOnHover :
	'onHover' '=' behaviors+=[DslUiBehavior] ("," behaviors+=[DslUiBehavior])*;

DslOnSelectItem :
	'onSelectItem' '=' behaviors+=[DslUiBehavior] ("," behaviors+=[DslUiBehavior])*;
	


	
DslColumnType :
	("Text"|"Button"|"Date"|"Custom"|"Link");
	
DslGuiEvent :
  (doc=STRING)? 
  "Event" name=ID "{"
    ((attributes+=DslGuiAttribute) )*
  "}";

DslGuiDto :
  (doc=STRING)? 
  "GuiDto" 'for' ("@"for=[mm::DslDomainObject]) "{"
  	((gapClass?="gap") | (noGapClass?="nogap"))?
  "}";


//////////////////////////////////////////////////////////////
// Behaviors
//////////////////////////////////////////////////////////////

DslUiBehavior :
	DslPopulatePanelBehavior | DslPopulateListBoxBehavior | DslNavigateToViewBehavior | DslUpdateTableBehavior | DslConfirmMessageBehavior
	| DslCustomBehavior | DslCompositeBehavior | DslMarkDirtyBehavior | DslMarkCleanBehavior | DslWarnIfDirtyBehavior
	| DslGoBackBehavior | DslUpdateAttributeBehavior | DslClearWidgetsBehavior | DslPopupPanelBehavior;
	
DslPopulatePanelBehavior :
  (doc=STRING)?
  "UiBehavior" "PopulatePanel" name=ID "{"
	("hint" "=" hint=STRING)?
    'panel' '=' panel=[DslPanelWidget]    
  "}";

DslPopulateListBoxBehavior :
  (doc=STRING)?
  "UiBehavior" "PopulateListBox" name=ID "{"
	("hint" "=" hint=STRING)?
    'listBox' '=' widget=[DslListBoxWidget]    
  "}";

DslNavigateToViewBehavior :
  (doc=STRING)?
  "UiBehavior" "NavigateToView" name=ID "{"
	("hint" "=" hint=STRING)?
    'view' '=' view=[DslView]
  "}";

DslUpdateTableBehavior :
  (doc=STRING)?
  "UiBehavior" "UpdateTable" name=ID "{"
	("hint" "=" hint=STRING)?
  	'table' '=' table=[DslTableWidget]
  "}";

DslConfirmMessageBehavior :
  (doc=STRING)?
  "UiBehavior" "ConfirmMessage" name=ID "{"
	("hint" "=" hint=STRING)?
  	('message' '=' message=STRING)?
  "}";

DslCustomBehavior :
  (doc=STRING)?
  "UiBehavior" "Custom" name=ID "{"
	("hint" "=" hint=STRING)?
  "}";

DslCompositeBehavior :
  (doc=STRING)?
  "UiBehavior" "Composite" name=ID "{"
	("hint" "=" hint=STRING)?
   	behaviors+=[DslUiBehavior] ("," behaviors+=[DslUiBehavior])*
  "}";
	
DslMarkDirtyBehavior :
  (doc=STRING)?
  "UiBehavior" "MarkDirty" name=ID "{"
	("hint" "=" hint=STRING)?
	((specificObject?="specificObject") | (NOT "specificObject"))?
  "}";

DslMarkCleanBehavior :
  (doc=STRING)?
  "UiBehavior" "MarkClean" name=ID "{"
	("hint" "=" hint=STRING)?
	((specificObject?="specificObject") | (NOT "specificObject"))?
  "}";

DslWarnIfDirtyBehavior :
  (doc=STRING)?
  "UiBehavior" "WarnIfDirty" name=ID "{"
	("hint" "=" hint=STRING)?
  	('message' '=' message=STRING)?
  "}";

DslGoBackBehavior :
  (doc=STRING)?
  "UiBehavior" "GoBack" name=ID "{"
	("hint" "=" hint=STRING)?
  "}";

DslUpdateAttributeBehavior :
  (doc=STRING)?
  "UiBehavior" "UpdateAttribute" name=ID "{"
	("hint" "=" hint=STRING)?
	("propertyPath" "=" propertyPath=STRING)?
	("targetClass" "=" targetClass=[mm::DslSimpleDomainObject])?
  "}";

DslClearWidgetsBehavior :
  (doc=STRING)?
  "UiBehavior" "ClearWidgets" name=ID "{"
	("hint" "=" hint=STRING)?
	('widgets' '=' widgets+=[DslWidget] ("," widgets+=[DslWidget])*)?
  "}";

DslPopupPanelBehavior :
  (doc=STRING)?
  "UiBehavior" "PopupPanel" name=ID "{"
	("hint" "=" hint=STRING)?
	("panelName" "=" panelName=STRING)
	('view' '=' view=[DslView])?
  "}";


//////////////////////////////////////////////////////////////
// Conditions
//////////////////////////////////////////////////////////////

DslUiCondition :
	DslUiCustomCondition | DslHasDirtyValueCondition;

DslUiCustomCondition :
  (doc=STRING)?
  "Condition" "Custom" name=ID "{"
	  ("hint" "=" hint=STRING)?
  "}";

DslHasDirtyValueCondition :
  (doc=STRING)?
  "Condition" "HasDirtyValue" name=ID "{"
	  ("hint" "=" hint=STRING)?
	  ((dirty?="dirty") | (NOT "dirty"))
  "}";




	
DslGuiAttribute :
  (doc=STRING)?
  (type=DslGuiType  name=ID
	) (";")?;

DslGuiType :
  ("String"|"int"|"Integer"|"long"|"Long"|"boolean"|"Boolean"|"Date"|"DateTime"|"Timestamp"|"BigDecimal"|"BigInteger"|
  	"double"|"Double"|"float"|"Float"|"Key"|"PagingPagarameter"|"PagedResult"|"Blob"|"Clob"|
  	DslGuiJavaIdentifier);

DslGuiJavaIdentifier :
  (ID ("." ID)*);

terminal REF :
  ('-'|'reference');

terminal NOT :
  ('!'|'not');
  
  
 


// TODO: This probably needs to be reduced down - not all are appropriate?
//DslGuiType :
//  ("String"|"int"|"Integer"|"long"|"Long"|"boolean"|"Boolean"|"Date"|"DateTime"|"Timestamp"|"BigDecimal"|"BigInteger"|
//  	"double"|"Double"|"float"|"Float"|"Key"|"PagingPagarameter"|"PagedResult"|"Blob"|"Clob"|
//  	DslGuiJavaIdentifier);

//DslGuiJavaIdentifier :
//  (ID ("." ID)*);
  
//DslGuiReference :
//  (doc=STRING)?
//  REF (collectionType=mm::DslCollectionType"<")? (("@")?domainObjectType=[mm::DslSimpleDomainObject]) (">")? name=ID
//    (((notChangeable?=NOT "changeable") | ("changeable"))? &
//     ((required?="required") | (NOT "required"))? &
//     (((nullable?="nullable") | (NOT "nullable")) ("=" nullableMessage=STRING)?)? &
//     ("hint" "=" hint=STRING)? &
//     ((notEmpty?="notEmpty") ("=" notEmptyMessage=STRING)?)? &
//     ("size" "=" size=STRING)? &
//     ((valid?="valid") ("=" validMessage=STRING)?)? &
//     ("validate" "=" validate=STRING)? )
//    (";")?;
    

//terminal REF :
//  ('-'|'reference');
//
//terminal NOT :
//  ('!'|'not');
//  
//  
// 