ApplicationPart Person {
 
    Module person {
        Service PersonService {
          findPersonByName => PersonRepository.findPersonByName;
          PagedResult<@Person> findPaged(String name, PagingParameter pagingParameter);
          List<@Person> findByDate(Date entryDate);
          
          send(String from, @CreatePersonCmd cmd);
        }
     
        Entity Person {
          gap
          scaffold
          Date birthDate
          - @Gender sex !changeable
          - @Ssn ssn key
          - @PersonName name

          def String getDisplayName();
                
          Repository PersonRepository {
              List<@Person> findPersonByName(String name) => AccessObject;
              save;
              save(Collection<@Person> entities);
              findByQuery;
              findByExample;
              findByKeys;
          }
        }
        
        BasicType Ssn {
          String number key length="20"
          - @Country country key
        }
    
        BasicType PersonName {
          String first
          String last
        }
        
        enum Gender {
            FEMALE("F"), 
            MALE("M")
        }
        
        enum Country {
        	SWEDEN,
        	NORWAY,
        	DENMARK
        }
        
        CommandEvent PersonCmd {
        	not persistent
        	
        	- @Ssn ssn required not nullable;
        	
        }
        
        CommandEvent CreatePersonCmd extends PersonCmd {
        	not persistent
        	
        	Date birthDate required not nullable;
        }
    }
  
   
}


